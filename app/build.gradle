apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def superpowered_sdk_path = properties.getProperty('superpowered.dir')

apply plugin: 'maven-publish'

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}
android {
    flavorDimensions "default"
    namespace = "com.juztoss.rhythmo"
    compileSdkVersion = 34
    buildToolsVersion = '34.0.0'

    defaultConfig {
        applicationId "com.juztoss.rhythmo"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 33
        versionName "1.0.16"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64' // these platforms cover 99% percent of all Android devices
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_ARM_NEON=TRUE', '-DANDROID_STL=c++_static', "-DPATH_TO_SUPERPOWERED:STRING=${superpowered_sdk_path}"
                cFlags '-O3', '-fsigned-char' // full optimization, char data type is signed
                cppFlags '-fsigned-char', "-I${superpowered_sdk_path}"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni']
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles.add(file("proguard-rules.pro"))
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    productFlavors {
        create("dev") {
            applicationId "com.juztoss.rhythmo.dev"
        }
        create("prod") {
            applicationId "com.juztoss.rhythmo"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.12.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs/')
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.11'
    implementation 'de.psdev.licensesdialog:library:1.8.2-SNAPSHOT'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.media:media:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'

    androidTestImplementation 'junit:junit:4.13.2'
}